buildscript {
    repositories {
        jcenter()
        mavenLocal()
    }
    dependencies {
        // Multi-OS Engine plugin
        // when it is updating you need to run "./gradlew moeUpdateXcodeSettings"
        classpath group: 'org.multi-os-engine', name: 'moe-gradle', version: '1.10.2'
    }
}

// Apply plugins
apply plugin: 'moe'

// Set source and target to Java 8
sourceCompatibility = "1.8"
targetCompatibility = "1.8"

// Set maven repository
repositories {
    mavenCentral()
}

// Exclude all files from Gradle's test runner
test { exclude '**' }

// Setup Multi-OS Engine
moe {
    xcode {
        project 'xcode/Quickstart.xcodeproj'
        mainTarget 'Quickstart'
        testTarget 'Quickstart-Test'
    }
}

dependencies {
    implementation project(':client')
    implementation 'org.swingme:swingme-binding-ios:1.0.0'
}

moeMainReleaseProGuard {
    appendCfgFile "proguard.append.cfg"
}
moeMainDebugProGuard {
    appendCfgFile "proguard.append.cfg"
}
moeTestDebugProGuard {
    appendCfgFile "proguard.append.cfg"
}

// copy the 'iosmeimg' folder into the main app resource bundle

// Default excludes are: ("LICENSE", "LICENSE.*", "META-INF/LICENSE", "META-INF/LICENSE.*", "NOTICE", "NOTICE.*", "META-INF/NOTICE", "META-INF/NOTICE.*", "**/.*")
moe.packaging.exclude("META-INF/moe-lib/**")

task extractFileFromJar(type: Copy) {
    def jarFile = configurations.runtimeClasspath.find { it.name.contains("swingme-binding-ios") }
    from zipTree(jarFile)
    include "META-INF/moe-lib/proguard.append.cfg"
    eachFile { file ->
        file.path = file.name
    }
    includeEmptyDirs = false
    into projectDir
}
task copyiOSresources(dependsOn: extractFileFromJar) {
    doLast {
        def subDir = "META-INF/moe-lib/ios-res/"
        configurations.runtimeClasspath.files.each { jar ->
            copy {
                from zipTree(jar)
                include "$subDir/**"
                into("xcode/")
                eachFile { file ->
                    file.path = file.path.replaceFirst("^$subDir", '')
                }
                includeEmptyDirs(false)
            }
        }
    }
}

moeMainReleaseIphoneosXcodeBuild.dependsOn copyiOSresources
moeMainDebugIphoneosXcodeBuild.dependsOn copyiOSresources
moeMainReleaseIphonesimulatorXcodeBuild.dependsOn copyiOSresources
moeMainDebugIphonesimulatorXcodeBuild.dependsOn copyiOSresources

if (System.getenv('PLATFORM_NAME') != null) {
    moeXcodeInternal.dependsOn copyiOSresources
}

