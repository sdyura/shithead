plugins {
    id 'java-library'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    implementation rootProject
    def lobbyServer = 'net.yura:lobby-server:1.0.0'
    compileOnly lobbyServer

    testImplementation lobbyServer
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    testImplementation 'org.mockito:mockito-core:5.20.0'
}

// To run the test server, execute the following command in the root project directory:
// ./gradlew --parallel :server:runTestServer
task runTestServer(type: JavaExec) {
    group = "Verification"
    description = "Runs the TestServer"
    mainClass = 'net.yura.shithead.server.TestServer'
    classpath = sourceSets.test.runtimeClasspath
}

jar {
    archiveFileName = 'shithead-server.jar'
    manifest {
        attributes(
            'Class-Path': configurations.runtimeClasspath.files.collect { it.name }.join(' ')
        )
    }
}

task copyDependencies(type: Copy) {
    from configurations.runtimeClasspath
    into "$buildDir/libs"
}

build.dependsOn copyDependencies

task deploy(dependsOn: build) {
    doLast {
        def lobbyServerDir = rootProject.file('../Lobby/dist/server/')
        if (!lobbyServerDir.exists()) {
            throw new GradleException("Lobby server directory not found at ${lobbyServerDir.absolutePath}")
        }

        def shitheadServerDir = new File(lobbyServerDir, 'Shithead')
        if (!shitheadServerDir.exists()) {
            println "Creating directory: ${shitheadServerDir.absolutePath}"
            shitheadServerDir.mkdirs()
        }

        copy {
            from "$buildDir/libs"
            into shitheadServerDir
            include '*.jar'
        }
        println "Copied JARs to ${shitheadServerDir.absolutePath}"
    }
}
